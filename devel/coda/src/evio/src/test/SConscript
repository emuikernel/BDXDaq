# scons build file for evio test
Import('env', 'binInstallDir', 'archDir', 'execLibs', 'debugSuffix')

# lists of all C and C++ files
cfileList  = Glob('*.c',  strings=True)
ccfileList = Glob('*.cc', strings=True)

# Compile C files and install executables if "scons install" is run
for fileName in cfileList:
    compiledFile = env.Program(source  = fileName,
                               LIBS    = ['evio'+debugSuffix] + execLibs,
                               LIBPATH = ['#src/libsrc/'+archDir],
                               CPPPATH = ['.', '#src/libsrc'])
    
    if 'install' in COMMAND_LINE_TARGETS:
        env.Alias('install', env.Install(target = binInstallDir+'/test', source = compiledFile))

# Compile C++ files and install
for fileName in ccfileList:
    compiledFile = env.Program(source  = fileName,
                               LIBS    = ['evio'+debugSuffix, 'evioxx'+debugSuffix] + execLibs,
                               LIBPATH = ['#src/libsrc/'+archDir, '#src/libsrc++/'+archDir],
                               CPPPATH = ['.', '#src/libsrc', '#src/libsrc++'])
    
    if 'install' in COMMAND_LINE_TARGETS:
        env.Alias('install', env.Install(target = binInstallDir+'/test', source = compiledFile))
